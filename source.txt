### Controls ####
# vpn config_name
################################
# Example: vpn work
# Would toggle connect/disconnect VPN using the configuration file named "work"
### END ####

on sendMsg(d)
	
	tell application "System Events"
		set isRunning to (count of (every process whose bundle identifier is "com.Growl.GrowlHelperApp")) > 0
	end tell
	
	if isRunning then
		set nm to "Tunnelblick Notifications"
		tell application id "com.Growl.GrowlHelperApp"
			set the allNotificationsList to {nm}
			set the enabledNotificationsList to {nm}
			
			register as application "Tunnelblick" all notifications allNotificationsList default notifications enabledNotificationsList icon of application "Tunnelblick"
			
			notify with name nm title "Tunnelblick" description d application name "Tunnelblick" icon of application "Tunnelblick"
			
		end tell
	end if
	
end sendMsg

on alfred_script(q)
	
	set msg to ""
	tell application "Tunnelblick"
		set configs to get name of configurations
		if q is in configs then
			set s to get state of first configuration where name = q
			if s is equal to "CONNECTED" then
				disconnect q
				set msg to "Disconnecting from " & q
			else
				activate
				connect q
				set msg to "Connecting to " & q
			end if
		else
			set msg to "Could not find a configuration for " & q
		end if
	end tell
	
	if msg is not equal to "" then
		sendMsg(msg)
	end if
	
end alfred_script